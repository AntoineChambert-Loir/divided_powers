/- Copyright 2022 ACL & MIdFF-/

import algebra.free_algebra
import algebra.ring_quot
import algebra.triv_sq_zero_ext
import algebra.algebra.operations
import linear_algebra.multilinear.basic

import ring_theory.graded_algebra.basic
import ring_theory.tensor_product
import data.mv_polynomial.supported

import data.rel

import divided_powers.basic
import divided_powers.sub_pd_ideal
import divided_powers.rat_algebra
import divided_powers.ideal_add
import ..weighted_homogeneous -- Modified version of PR #17855
import ..graded_ring_quot -- Quotients of graded rings
import ..graded_module_quot

noncomputable theory

/-! 
The divided power algebra of a module -/

open finset mv_polynomial ring_quot direct_sum ideal ideal.quotient

theorem ideal.pow_eq_bot {R : Type*} [comm_semiring R] [no_zero_divisors R] {I : ideal R} 
  {n : ℕ} (hn : n ≠ 0) :
  I ^ n = ⊥ ↔ I = ⊥ :=
begin
  induction n with n ih,
  { exfalso, exact hn (eq.refl _) },
  { by_cases hn0 : n = 0,
    { rw [hn0, pow_one] },
    { rw [pow_succ, mul_eq_bot, ih hn0, or_self] }}
end

namespace mv_polynomial

variables {R S σ : Type*} [comm_semiring R] [comm_semiring S] 

@[simp] lemma eval₂_hom.smul (f : R →+* S) (g : σ → S) (r : R) (P : mv_polynomial σ R) :
  eval₂_hom f g (r • P) = f r • eval₂_hom f g P := 
by simp only [smul_eq_C_mul, coe_eval₂_hom, eval₂_mul, eval₂_C, algebra.id.smul_eq_mul]

variables [algebra R S]

variable (R)
/-- `mv_polynomial.eval₂ (algebra_map R S) g` as an `R`-algebra homomorphism. -/
def eval₂_alg_hom  (g : σ → S) :
  mv_polynomial σ R →ₐ[R] S := 
{ commutes' := λ r, by rw [ring_hom.to_fun_eq_coe, coe_eval₂_hom, algebra_map_eq, eval₂_C], 
  .. eval₂_hom (algebra_map R S) g }

variable {R}
lemma eval₂_alg_hom_apply (g : σ → S) (P : mv_polynomial σ R) :
  eval₂_alg_hom R g P = eval₂_hom (algebra_map R S) g P := rfl

@[simp] lemma coe_eval₂_alg_hom (g : σ → S) :
  ⇑(eval₂_alg_hom R g) = eval₂ (algebra_map R S) g := rfl

@[simp] lemma eval₂_alg_hom_X' (g : σ → S) (i : σ) :
  eval₂_alg_hom R g ((X i : mv_polynomial σ R)) = g i := 
eval₂_X (algebra_map R S)  g i

end mv_polynomial

section ideals_and_rel

lemma quotient_mk_eq_of_rel {A : Type*} [comm_ring A] {r : A → A → Prop} {a b : A} (h : r a b) :
  mk (of_rel r) a = mk (of_rel r) b :=
begin
  suffices hinj : function.injective (ring_quot.ring_quot_equiv_ideal_quotient r).inv_fun,
  { apply hinj, exact mk_ring_hom_rel h },
  exact function.injective_iff_has_left_inverse.mpr ⟨(ring_quot_equiv_ideal_quotient r).to_fun,
    (ring_quot_equiv_ideal_quotient r).right_inv⟩,
end

namespace ideal

lemma quotient_mk_eq_ring_quot_apply (R : Type*) [comm_ring R] {A : Type*} [comm_ring A]
  [algebra R A] (r : A → A → Prop) (a : A) :
  mk (of_rel r) a = ring_quot_to_ideal_quotient r (mk_alg_hom R r a) :=
by rw [← ring_quot_to_ideal_quotient_apply r a, ← mk_alg_hom_coe R r];  refl

namespace quotient 

variables {R S : Type*} [comm_ring R] [comm_ring S]

lemma rel_le_ker (I : ideal R) {r : R → R → Prop} (hr : I = of_rel r) (f : R →+* S) 
  (hf : ∀ {a b : R}, r a b → f a = f b) : I ≤ f.ker :=
begin
  rw [hr, of_rel, submodule.span_le],
  rintros x ⟨a, b, hx, hab⟩,
  rw [eq_sub_iff_add_eq.mpr hab, set_like.mem_coe, ring_hom.mem_ker, map_sub, sub_eq_zero, hf hx]
end

/-- Given a binary relation `r` on `R` and a ring homomorphism `f : R →+* S` that is constant on
  each equivalent class of `r`, lift `f` to the quotient by the ideal generated by `r`. -/
def lift_rel  (I : ideal R) {r : R → R → Prop} (hr : I = of_rel r) (f : R →+* S)
  (hf : ∀ (a b : R), r a b → f a = f b) : R ⧸ I →+* S :=
lift I f (rel_le_ker I hr f hf)

end quotient

end ideal

end ideals_and_rel

namespace triv_sq_zero_ext

variables (R M : Type*) [comm_semiring R] [add_comm_monoid M] [module R M] [module Rᵐᵒᵖ M]
  [is_central_scalar R M]

def ker_ideal : ideal (triv_sq_zero_ext R M) := ring_hom.ker (fst_hom R R M)

lemma mem_ker_ideal_iff_inr (x : triv_sq_zero_ext R M) :
  (x ∈ ker_ideal R M ↔ x = inr x.snd) :=
begin
  obtain ⟨r, m⟩ := x,
  simp only [ker_ideal, ring_hom.mem_ker, fst_hom_apply, fst_mk],
  exact ⟨λ hr, by {rw hr, refl}, λ hrm, by rw [← fst_mk r m, hrm, fst_inr]⟩,
end

lemma mem_ker_ideal_iff_exists (x : triv_sq_zero_ext R M) :
  (x ∈ ker_ideal R M ↔ ∃ (m : M), x = inr m) :=
by rw mem_ker_ideal_iff_inr; exact ⟨λ h, ⟨x.snd, h⟩, λ ⟨m, hm⟩, by {rw hm, refl}⟩


lemma square_zero : (ker_ideal R M) ^ 2 = 0 := 
begin
  simp only [pow_two, zero_eq_bot, eq_bot_iff, mul_le, mem_ker_ideal_iff_inr],
  rintros x hx y hy, 
  rw [hx, hy, mem_bot, inr_mul_inr],
end

end triv_sq_zero_ext

open ideal ideal.quotient triv_sq_zero_ext

section graded_algebra

variables {R : Type*} [comm_ring R]
variables {A : Type*} [comm_ring A] [algebra R A]
variables {ι : Type*}[canonically_ordered_add_monoid ι]
variables (𝒜 : ι → submodule R A)

lemma grade_zero_coe_smul (r : R) (x : 𝒜 0) : (↑(r • x) : A) = r • x := rfl 

variables  [decidable_eq ι] [graded_algebra 𝒜]
instance : has_one ↥(𝒜 0) := 
⟨⟨1, (@graded_ring.to_graded_monoid ι A (submodule R A) _ _ _ _ _ 𝒜 _).one_mem⟩⟩

instance : has_mul ↥(𝒜 0) := 
⟨λ x y, ⟨x * y, by convert set_like.mul_mem_graded x.2 y.2; rw [add_zero]⟩⟩

@[simp] lemma grade_zero_coe_mul (x y : 𝒜 0) : (↑(x * y) : A) = x * y := rfl 

@[simp] lemma grade_zero_val_mul (x y : 𝒜 0) : (x * y).val = x.val * y.val := rfl

@[simp] lemma grade_zero_coe_one : (↑(1 : 𝒜 0) : A) = 1 := rfl

lemma one_mem : (1 : A) ∈ 𝒜 0 := set_like.one_mem_graded 𝒜

example : add_comm_monoid (𝒜 0) := infer_instance

example : has_neg (𝒜 0) := add_subgroup_class.has_neg

instance grade_zero_comm_ring : comm_ring ↥(𝒜 0) := { 
  add           := (+),
  zero          := 0,
  neg           := has_neg.neg,
  one           := 1,
  mul           := (*),
  mul_assoc     := λ x y z, by ext; simp only [grade_zero_coe_mul, mul_assoc],
  one_mul       := λ x, by  ext; rw [grade_zero_coe_mul, grade_zero_coe_one, one_mul],
  mul_one       := λ x, by  ext; rw [grade_zero_coe_mul, grade_zero_coe_one, mul_one],
  left_distrib  := λ x y z, by ext; simp only [submodule.coe_add, grade_zero_coe_mul, left_distrib],
  right_distrib := λ x y z, 
    by ext; simp only [submodule.coe_add, grade_zero_coe_mul, right_distrib],
  mul_comm      := λ x y, by ext; simp only [grade_zero_coe_mul, mul_comm],
  ..(infer_instance : add_comm_group (𝒜 0)) }

instance grade_zero_algebra : algebra R ↥(𝒜 0) := algebra.of_module'
  (λ r x, by ext; simp only [grade_zero_coe_mul, grade_zero_coe_smul, grade_zero_coe_one, 
    algebra.smul_mul_assoc, one_mul])
  (λ r x, by ext; simp only [grade_zero_coe_mul, grade_zero_coe_smul, grade_zero_coe_one, 
    algebra.mul_smul_comm, mul_one])

/-- The projection from `A` to the degree `i` component `𝒜 i`, as an `R`-linear map. -/
def proj (i : ι) : A →ₗ[R] (𝒜 i) :=
{ to_fun    := λ a, decompose 𝒜 a i,
  map_add'  := λ a b, by rw [decompose_add, add_apply],
  map_smul' := λ r a, by rw [decompose_smul, dfinsupp.coe_smul, pi.smul_apply, ring_hom.id_apply] }

@[simps] def proj_zero_ring_hom' : A →+* (𝒜 0) :=
{ to_fun := λ a, proj 𝒜 0 a,
  map_one' := begin 
    ext,
    simp only [proj, linear_map.coe_mk, decompose_of_mem_same 𝒜 (one_mem 𝒜),
    grade_zero_coe_one], 
  end,
  map_zero' := by { simp only [proj, decompose_zero, linear_map.coe_mk, zero_apply] },
  map_add' := λ _ _, by { simp only [proj, decompose_add, linear_map.coe_mk, add_apply] },
  map_mul' := λ x y, begin
    ext,
    simp only [proj, linear_map.coe_mk, set_like.coe_eq_coe, grade_zero_coe_mul, 
      ← graded_ring.proj_zero_ring_hom_apply 𝒜, ← _root_.map_mul],
  end }

end graded_algebra
section

variables {R M : Type*} [comm_ring R]

instance [decidable_eq R] [decidable_eq M] :
  graded_algebra (weighted_homogeneous_submodule R (prod.fst : ℕ × M → ℕ)) := 
weighted_graded_algebra _ _

variable {R}
def degree (v : (ℕ × M) →₀ ℕ) : ℕ := finsum (λ x, (v x) * x.1)

def is_homogeneous_of_degree (p : mv_polynomial (ℕ × M) R) (n : ℕ) : Prop :=
∀ v ∈ p.support, degree v = n 

variables (R M) [add_comm_group M] [module R M]

namespace divided_power_algebra

-- We should probably change this name...

/-- The type coding the basic relations that will give rise to the divided power algebra. 
  The class of X (n, a) will be equal to dpow n a, with a ∈ M. --/
inductive rel : (mv_polynomial (ℕ × M) R) → (mv_polynomial (ℕ × M) R) → Prop 
  --rel (mv_polynomial (ℕ × M) R) (mv_polynomial (ℕ × M) R) Q : Why not use rel?
-- force `ι` to be linear and creates the divided powers
| zero {a : M} : rel (X (0, a)) 1
| smul {r : R} {n : ℕ} {a : M} : rel (X (n, r • a)) (r^n • X (n, a))
| mul {m n : ℕ} {a : M} : rel (X (m, a) * X (n, a)) ((nat.choose (m + n) m) • X (m + n, a))
| add {n : ℕ} {a b : M} : rel (X (n, a + b)) 
    (finset.sum (range (n + 1)) (λ k, (X (k, a) * X (n - k, b))))

/-- The ideal of mv_polynomial (ℕ × M) R generated by rel -/
def relI : ideal (mv_polynomial (ℕ × M) R) := of_rel (rel R M)

end divided_power_algebra

/-- The divided power algebra of a module M is the quotient of the polynomial ring
by the ring relation defined by divided_power_algebra.rel -/
@[protected, derive [inhabited, comm_ring, algebra R]]
def divided_power_algebra : Type* :=
 (mv_polynomial (ℕ × M) R) ⧸ (divided_power_algebra.relI R M)

namespace divided_power_algebra

/- Note that also we don't know yet that `divided_power_algebra R M` has divided powers, 
  it has a kind of universal property for morphisms to a ring with divided_powers -/

open mv_polynomial

/-- If `R` is a `k`-algebra, then `divided_power_algebra R M` inherits a `k`-algebra structure. -/
def algebra' (k : Type*) [comm_ring k] [algebra k R] : 
  algebra k (divided_power_algebra R M) :=
ring_hom.to_algebra (ring_hom.comp (algebra_map R (divided_power_algebra R M)) (algebra_map k R))

variables {R M}

lemma sub_mem_rel_of_rel {a b : mv_polynomial (ℕ × M) R} (h : rel R M a b) :  a - b ∈ relI R M :=
submodule.subset_span ⟨a, b, h, by rw [sub_add_cancel]⟩

variable (R)
/-- `dp R n m` is the equivalence class of `X (⟨n, m⟩)` in `divided_power_algebra R M`. -/
def dp (n : ℕ) (m : M) : divided_power_algebra R M :=
mkₐ R (relI R M) (X (⟨n, m⟩))

lemma dp_eq_mkₐ (n : ℕ) (m : M) : dp R n m = (mkₐ R (relI R M)) (X ⟨n, m⟩) := rfl

lemma dp_eq_mk (n : ℕ) (m : M) : dp R n m = (mk (relI R M)) (X (⟨n, m⟩ : ℕ × M)) :=
by rw [dp, mkₐ_eq_mk]

 -- { rw [dp, mkₐ_eq_mk, prod.mk.eta] },

lemma dp_zero (m : M) : dp R 0 m = 1 :=
begin
  rw [dp, mkₐ_eq_mk, ← map_one (ideal.quotient.mk (relI R M)), ideal.quotient.eq],
  exact submodule.subset_span ⟨X(0,m), 1, rel.zero, by rw sub_add_cancel⟩
end

lemma dp_smul (r : R) (n  : ℕ) (m : M) : dp R n (r • m) = r ^ n • dp R n m :=
begin
  rw [dp, dp, ← map_smul, mkₐ_eq_mk R, ideal.quotient.eq], 
  exact sub_mem_rel_of_rel rel.smul,
end

lemma dp_mul (n p : ℕ) (m : M) : dp R n m * dp R p m = (n + p).choose n • dp R (n + p) m :=
begin
  simp only [dp, mkₐ_eq_mk, ← _root_.map_mul, ← map_nsmul, ideal.quotient.eq], 
  exact sub_mem_rel_of_rel rel.mul,
end

lemma dp_add (n : ℕ) (x y : M) : dp R n (x + y) = 
  (range (n+1)).sum (λ k, dp R k x * dp R (n - k) y) := 
begin
  simp only [dp, mkₐ_eq_mk, ← _root_.map_mul, ← map_sum, ideal.quotient.eq], 
  exact sub_mem_rel_of_rel rel.add,
end

lemma unique_on_dp {A : Type*} [comm_ring A] [algebra R A]
  {f g : divided_power_algebra R M →ₐ[R] A} (h : ∀ n m, f (dp R n m) = g (dp R n m)) :
  f = g := 
begin
  rw fun_like.ext'_iff,
  apply function.surjective.injective_comp_right (quotient.mkₐ_surjective R (relI R M)),
  simp only [←  alg_hom.coe_comp, ← fun_like.ext'_iff], 
  exact alg_hom_ext (λ ⟨n, m⟩, h n m)
end

section functoriality

variables (R M)

section lift

variables {A : Type*} [comm_ring A] [algebra R A]

/- General purpose lifting lemma -/
lemma lift_rel_le_ker (f : ℕ × M → A) (hf_zero : ∀ m, f (0, m) = 1)
  (hf_smul : ∀ (n : ℕ) (r : R) (m : M), f ⟨n, r • m⟩ = r ^ n • f(⟨n, m⟩)) 
  (hf_mul : ∀ n p m, f ⟨n, m⟩ * f ⟨p, m⟩ = ((n + p).choose n) • f ⟨n + p, m⟩)
  (hf_add : ∀ n u v, f ⟨n, u + v⟩ = (range (n + 1)).sum (λ (x : ℕ), f ⟨x, u⟩ * f ⟨n - x, v⟩)) :
  relI R M ≤ ring_hom.ker (@eval₂_alg_hom R A (ℕ × M) _ _ _ f) := 
begin
  rw [relI, of_rel, submodule.span_le],
  rintros x ⟨a, b, hx, hab⟩,
  rw [eq_sub_iff_add_eq.mpr hab, set_like.mem_coe, ring_hom.mem_ker, map_sub, sub_eq_zero],
  induction hx with m r n m n p m n u v,
  { rw [eval₂_alg_hom_X', map_one, hf_zero] },
  { simp only [eval₂_alg_hom_X', alg_hom.map_smul, hf_smul] },
  { simp only [_root_.map_mul, eval₂_alg_hom_X', nsmul_eq_mul, map_nat_cast, hf_mul] },
  { simp only [coe_eval₂_alg_hom, eval₂_X, eval₂_sum, eval₂_mul, hf_add] },
end

/-- General purpose universal property of `divided_power_algebra R M` -/
def lift_aux (f : ℕ × M → A) (hf_zero : ∀ m, f (0, m) = 1) 
  (hf_smul : ∀ (n : ℕ) (r : R) (m : M), f ⟨n, r • m⟩ = r ^ n • f(⟨n, m⟩)) 
  (hf_mul : ∀ n p m, f ⟨n, m⟩ * f ⟨p, m⟩ = ((n + p).choose n) • f ⟨n + p, m⟩)
  (hf_add : ∀ n u v, f ⟨n, u + v⟩ = (range (n + 1)).sum (λ (x : ℕ), f ⟨x, u⟩ * f ⟨n - x, v⟩)) : 
  divided_power_algebra R M →ₐ[R] A :=
liftₐ (relI R M) (eval₂_alg_hom R f) (lift_rel_le_ker R M f hf_zero hf_smul hf_mul hf_add)

lemma lift_aux_eq (f : ℕ × M → A) (hf_zero : ∀ m, f (0, m) = 1) 
  (hf_smul : ∀ (n : ℕ) (r : R) (m : M), f ⟨n, r • m⟩ = r ^ n • f(⟨n, m⟩)) 
  (hf_mul : ∀ n p m, f ⟨n, m⟩ * f ⟨p, m⟩ = ((n + p).choose n) • f ⟨n + p, m⟩)
  (hf_add : ∀ n u v, f ⟨n, u + v⟩ = (range (n + 1)).sum (λ (x : ℕ), f ⟨x, u⟩ * f ⟨n - x, v⟩)) 
  (p : mv_polynomial (ℕ × M) R) :
  lift_aux R M f hf_zero hf_smul hf_mul hf_add (mkₐ R (relI R M) p) = eval₂ (algebra_map R A) f p :=
by simp only [lift_aux, mkₐ_eq_mk, liftₐ_apply, lift_mk, alg_hom.coe_to_ring_hom, coe_eval₂_alg_hom]

lemma lift_aux_eq_X (f : ℕ × M → A) (hf_zero : ∀ m, f (0, m) = 1) 
  (hf_smul : ∀ (n : ℕ) (r : R) (m : M), f ⟨n, r • m⟩ = r ^ n • f(⟨n, m⟩)) 
  (hf_mul : ∀ n p m, f ⟨n, m⟩ * f ⟨p, m⟩ = ((n + p).choose n) • f ⟨n + p, m⟩)
  (hf_add : ∀ n u v, f ⟨n, u + v⟩ = (range (n + 1)).sum (λ (x : ℕ), f ⟨x, u⟩ * f ⟨n - x, v⟩))  
  (n : ℕ) (m : M) :
  lift_aux R M f hf_zero hf_smul hf_mul hf_add (mkₐ R (relI R M) (X (n, m))) = f ⟨n, m⟩ :=
by rw [lift_aux_eq, eval₂_X]

variables {I : ideal A} (hI : divided_powers I) (φ : M →ₗ[R] A) (hφ : ∀ m, φ m ∈ I)

/-- The “universal” property of divided_power_algebra -/
def lift  : divided_power_algebra R M →ₐ[R] A :=
lift_aux R M (λ nm, hI.dpow nm.1 (φ nm.2)) (λ m, hI.dpow_zero (hφ m)) 
  (λ n r m, by rw [linear_map.map_smulₛₗ, ring_hom.id_apply, ← algebra_map_smul A r (φ m),
    smul_eq_mul, hI.dpow_smul n (hφ m), ← smul_eq_mul, ← map_pow, algebra_map_smul]) 
  (λ n p m, by rw [hI.dpow_mul n p (hφ m), ← nsmul_eq_mul]) 
  (λ n u v, by rw [map_add, hI.dpow_add n (hφ u) (hφ v)]) 

lemma lift_eqₐ (p : mv_polynomial (ℕ × M) R) : lift R M hI φ hφ (mkₐ R (relI R M) p) = 
  eval₂ (algebra_map R A) (λ (nm : ℕ × M), hI.dpow nm.1 (φ nm.2)) p :=
by rw [lift, lift_aux_eq]

lemma lift_eq (p : mv_polynomial (ℕ × M) R) : lift R M hI φ hφ (mk (relI R M) p) = 
  eval₂ (algebra_map R A) (λ (nm : ℕ × M), hI.dpow nm.1 (φ nm.2)) p :=
by rw [← mkₐ_eq_mk R, lift_eqₐ]

lemma lift_eqₐ_X (n : ℕ) (m : M) :
  lift R M hI φ hφ (mkₐ R (relI R M) (X (n, m))) = hI.dpow n (φ m) :=
by rw [lift, lift_aux_eq_X]

lemma lift_eq_X (n : ℕ) (m : M) :
  lift R M hI φ hφ (mk (relI R M) (X (n, m))) = hI.dpow n (φ m) :=
by rw [← mkₐ_eq_mk R, lift_eqₐ_X]

end lift

section lift'

variables {M}

variables (S : Type*) [comm_ring S] [algebra R S] {N : Type*} [add_comm_group N] [module R N]
  [module S N] [is_scalar_tower R S N] [algebra R (divided_power_algebra S N)]
  [is_scalar_tower R S (divided_power_algebra S N)] (f : M →ₗ[R] N) 

lemma lift'_rel_le_ker :
  relI R M ≤ ring_hom.ker (@eval₂_alg_hom R _ (ℕ × M) _ _ _ (λ nm, dp S nm.1 (f nm.2))) := 
begin
  apply rel_le_ker (relI R M) rfl,
  intros a b hab,
  induction hab with m r n m n p m n u v,
  { simp only [coe_eval₂_hom, eval₂_X, eval₂_one],
    rw dp_zero,  },
  { conv_rhs {rw [← eval₂_alg_hom_apply, map_smul],},
    simp only [eval₂_alg_hom_apply, eval₂_hom_X', linear_map.map_smul],
    rw [← algebra_map_smul S r, ← algebra_map_smul S (r ^ n), dp_smul, map_pow],
    apply_instance, apply_instance, },
  { simp only [coe_eval₂_hom, eval₂_mul, eval₂_X, nsmul_eq_mul], 
    simp only [eval₂_eq_eval_map, map_nat_cast, ← nsmul_eq_mul],
    rw dp_mul, },
  { simp only [map_add, coe_eval₂_hom, eval₂_sum, eval₂_mul, eval₂_X],
    rw dp_add, },
end

/-- The functoriality map between divided power algebras associated with a linear map of the
  underlying modules. Given an `R`-algebra `S`, an `S`-module `N` and `f : M →ₗ[R] N`, this is the
  map `divided_power_algebra R M →ₐ[R] divided_power_algebra S N` that maps `X(n,m)` to `X(n, f m)`.
-/
def lift' : divided_power_algebra R M →ₐ[R] divided_power_algebra S N := 
liftₐ (relI R M) _ (lift'_rel_le_ker R S f)

lemma lift'_eq (p : mv_polynomial (ℕ × M) R) : lift' R S f (mk (relI R M) p) = 
  eval₂ (algebra_map R (divided_power_algebra S N)) (λ nm : ℕ × M, dp S nm.1 (f nm.2)) p := 
by simp only [lift', liftₐ_apply, lift_mk, alg_hom.coe_to_ring_hom, coe_eval₂_alg_hom]

lemma lift'_eqₐ (p : mv_polynomial (ℕ × M) R) : lift' R S f (mkₐ R (relI R M) p) = 
  eval₂ (algebra_map R (divided_power_algebra S N)) (λ nm : ℕ × M, dp S nm.1 (f nm.2)) p := 
by rw [mkₐ_eq_mk, lift'_eq]

end lift'

end functoriality

section decidable_eq

variables (M) [decidable_eq R] [decidable_eq M]

lemma relI_is_homogeneous : 
  (relI R M).is_homogeneous ((weighted_homogeneous_submodule R (prod.fst : ℕ × M → ℕ))) :=
begin
  apply is_homogeneous_span,
  rintros x ⟨a, b, h, hx⟩,
  rw eq_sub_iff_add_eq.mpr hx,
  induction h with n r n m n p m n u v,
  { exact ⟨0, submodule.sub_mem _ (is_weighted_homogeneous_X R _ _) 
      (is_weighted_homogeneous_one R _)⟩ },
  { exact ⟨n, submodule.sub_mem _ (is_weighted_homogeneous_X R _ _) 
      (submodule.smul_mem _ _ (is_weighted_homogeneous_X R _ _))⟩ },
  { exact ⟨n + p, submodule.sub_mem _ (is_weighted_homogeneous.mul (is_weighted_homogeneous_X R _ _)
      (is_weighted_homogeneous_X R _ _)) (nsmul_mem (is_weighted_homogeneous_X R _ _) _)⟩ },
  { use n,
    refine submodule.sub_mem _ (is_weighted_homogeneous_X R _ _) (submodule.sum_mem _ _),
    intros c hc,
    have hnc : n = c + (n - c) := (nat.add_sub_of_le (finset.mem_range_succ_iff.mp hc)).symm, 
    nth_rewrite 1 hnc, 
    exact (is_weighted_homogeneous.mul (is_weighted_homogeneous_X R _ _)
      (is_weighted_homogeneous_X R _ _)) },
end

/-- The graded submodules of `divided_power_algebra R M` -/
def grade := quot_submodule R (weighted_homogeneous_submodule R (prod.fst : ℕ × M → ℕ))
  (divided_power_algebra.relI R M)

lemma one_mem : (1 : divided_power_algebra R M) ∈ grade R M 0 :=
⟨1, (is_weighted_homogeneous_one R _), rfl⟩

/-- The canonical decomposition of `divided_power_algebra R M` -/
def decomposition := 
quot_decomposition R (weighted_homogeneous_submodule R (prod.fst : ℕ × M → ℕ)) 
  (divided_power_algebra.relI R M) (relI_is_homogeneous R M)

end decidable_eq

end divided_power_algebra

/-- The graded algebra structure on the divided power algebra-/
def divided_power_galgebra [decidable_eq R] [decidable_eq M] :
  graded_algebra (divided_power_algebra.grade R M) := 
graded_quot_alg R (weighted_homogeneous_submodule R (prod.fst : ℕ × M → ℕ)) 
  (divided_power_algebra.relI R M) (divided_power_algebra.relI_is_homogeneous R M)

lemma mv_polynomial.vars_X_subset {R : Type*} {σ : Type*} (n : σ) [comm_semiring R] :
  (X n : mv_polynomial σ R).vars ⊆ {n} := 
begin
  classical,
  intro u,
  rw [X, mem_vars, mem_singleton], 
  rintro ⟨c, hc, hc'⟩,
  by_contradiction h', 
  rw [mem_support_iff, coeff_monomial, ne.def] at hc, 
  by_cases h : finsupp.single n 1 = c,
  { rw [← h, finsupp.mem_support_iff, ne.def, finsupp.single_apply] at hc',
    apply hc', rw if_neg (ne.symm h'), },
  { apply hc, rw if_neg h, },
end

namespace divided_power_algebra

open mv_polynomial

section decidable_eq

variables (M) [decidable_eq R] [decidable_eq M]

lemma mkₐ_mem_grade (n : ℕ) (m : M) : (mkₐ R (relI R M)) (X (n, m)) ∈ grade R M n :=
⟨X (n, m), is_weighted_homogeneous_X R _ (n, m), rfl⟩

/-- degree of a product is sum of degrees -/
lemma mul_mem ⦃i j : ℕ⦄ {gi gj : divided_power_algebra R M} (hi : gi ∈ grade R M i)
  (hj : gj ∈ grade R M j) : gi * gj ∈ grade R M (i + j) :=
(divided_power_galgebra R M).to_graded_monoid.mul_mem hi hj

def decompose : divided_power_algebra R M → direct_sum ℕ (λ (i : ℕ), ↥(grade R M i)) :=
(divided_power_galgebra R M).to_decomposition.decompose'

/- graded_algebra (grade R M )-/
instance : graded_algebra (divided_power_algebra.grade R M) := 
divided_power_galgebra R M

end decidable_eq

variable {M}

lemma surjective_of_supported : function.surjective ((mkₐ R (relI R M)).comp 
  (subalgebra.val (supported R {nm : ℕ × M | 0 < nm.1 }))) := 
begin
  intro f, 
  obtain ⟨p',hp'⟩ := mk_surjective f,
  have hX : ∀ (nm : ℕ × M), 0 < nm.1 → X nm ∈ supported R {nm : ℕ × M | 0 < nm.1},
  { intros nm hnm,
    rw mem_supported,
    refine set.subset.trans (finset.coe_subset.mpr (vars_X_subset nm)) _,
    rw [coe_singleton, set.singleton_subset_iff, set.mem_set_of_eq],
    exact hnm, },
  let φ : mv_polynomial (ℕ × M) R →ₐ[R] supported R {nm : ℕ × M | 0 < nm.1} :=  
    aeval (λ (nm: ℕ × M), dite (0 < nm.1) (λ h, ⟨(X nm), hX nm h⟩) (λ h, 1)),
  have hφ : (mkₐ R (relI R M)).comp ((subalgebra.val _).comp φ) = (mkₐ R _),
  { apply mv_polynomial.alg_hom_ext,
    rintro ⟨n,m⟩,
    simp only [alg_hom.coe_comp, mkₐ_eq_mk, subalgebra.coe_val, function.comp_app, aeval_X],
    split_ifs,
    { refl },
    { simp only [not_lt, le_zero_iff] at h,
      dsimp only [relI],
      rw [h, algebra_map.coe_one, quotient_mk_eq_of_rel rel.zero] }},
  use φ p',
  rw [← alg_hom.comp_apply, alg_hom.comp_assoc, hφ,  ← hp', mkₐ_eq_mk]
end

/-- The canonical linear map `M →ₗ[R] divided_power_algebra R M`. -/
def ι : M →ₗ[R] (divided_power_algebra R M) :=
{ to_fun    := λ m, dp R 1 m,
  map_add'  := λ x y, by simp only [dp_add, sum_range_succ', sum_range_zero, zero_add, nat.sub_zero,
    nat.sub_self, dp_zero, mul_one, one_mul],
  map_smul' := λ r x, by rw [dp_smul, pow_one, ring_hom.id_apply] }

lemma mk_alg_hom_mv_polynomial_ι_eq_ι (m : M) : mkₐ R (relI R M) (X (1, m)) = ι R m := rfl

lemma mk_alg_hom_mv_polynomial_ι_eq_ι' (m : M) : dp R 1 m = ι R m := rfl

@[simp] theorem ι_comp_lift {A : Type*} [comm_ring A] [algebra R A] {I : ideal A} 
  (hI : divided_powers I) (φ : M →ₗ[R] A) (hφ : ∀ m, φ m ∈ I) :
  (lift R M hI φ hφ).to_linear_map.comp (ι R) = φ :=
begin
  ext,
  rw [linear_map.coe_comp, function.comp_app, alg_hom.to_linear_map_apply,
    ← mk_alg_hom_mv_polynomial_ι_eq_ι, lift_eqₐ, eval₂_X],
  exact hI.dpow_one (hφ x),
end

@[simp] theorem lift_ι_apply {A : Type*} [comm_ring A] [algebra R A] {I : ideal A}
  (hI : divided_powers I) (φ : M →ₗ[R] A) (hφ: ∀ m, φ m ∈ I) (x) :
  lift R M hI φ hφ (ι R x) = φ x :=
by { conv_rhs {rw ← ι_comp_lift R hI φ hφ,},refl, }

variables (M)

def galg_hom.is_homogeneous {ι : Type*} /- [add_comm_monoid ι] [decidable_eq ι] -/
  {A : Type*} [comm_ring A] [algebra R A] (𝒜 : ι → submodule R A) /- [graded_algebra 𝒜] -/
  {B : Type*} [comm_ring B] [algebra R B] (ℬ : ι → submodule R B) /- [graded_algebra ℬ]  -/
  (f : A →ₐ[R] B):= 
∀ i a, a ∈ 𝒜 i → f a ∈ ℬ i

lemma finsupp.prod.mem_grade {A : Type*} [comm_ring A] [algebra R A] (𝒜 : ℕ → submodule R A)
  [graded_algebra 𝒜] {σ : Type*} (c : σ →₀ ℕ) (f : σ → A) (d : σ → ℕ)
  (hc : ∀ s ∈ c.support, f s ∈ 𝒜 (d s)) : 
  c.prod (λ s e, (f s) ^ e) ∈ 𝒜 (c.sum (λ s e, e * d s)) := 
begin
  classical,
  rw [finsupp.prod, finsupp.sum],
  let p : finset σ → Prop := 
  λ s, s ⊆ c.support → (s.prod (λ i, (f i) ^ c i) ∈ 𝒜 (s.sum (λ i, c i * d i))),
  apply @finset.induction_on σ p _ c.support,
  { exact imp_intro (set_like.one_mem_graded 𝒜) },
  { intros a s ha hs,
    by_cases hs' : (insert a s) ⊆ c.support,  
    { apply imp_intro,
      rw [finset.prod_insert ha, finset.sum_insert ha],
      exact set_like.mul_mem_graded (set_like.pow_mem_graded _ (hc a (hs' (mem_insert_self a s))))
       (hs (subset_trans (subset_insert a s) hs')) },
    { exact not.elim hs' }},
  { exact subset_rfl }
end

variable {R}
/-  [graded_algebra 𝒜] --not used in this def -/
def has_graded_dpow {A : Type*} [comm_ring A] [algebra R A] (𝒜 : ℕ → submodule R A)
  {I : ideal A} (hI : divided_powers I) := 
∀ (a : A) (ha : a ∈ I) (i : ℕ) (hai : a ∈ 𝒜 i) (n : ℕ), hI.dpow n a ∈ 𝒜 (n • i)

section decidable_eq

variables (R) [decidable_eq R] [decidable_eq M]

lemma lift_aux_is_homogeneous {A : Type*} [comm_ring A] [algebra R A] (𝒜 : ℕ → submodule R A) 
  [graded_algebra 𝒜] (f : ℕ × M → A) (hf_zero : ∀ m, f (0, m) = 1) 
  (hf_smul : ∀ (n : ℕ) (r : R) (m : M), f(⟨n, r • m⟩) = r ^ n • f(⟨n, m⟩)) 
  (hf_mul : ∀ n p m, f (⟨n, m⟩) * f (⟨p, m⟩) = ((n + p).choose n) • f (⟨n + p, m⟩))
  (hf_add : ∀ n u v, f (⟨n, u + v⟩) = (range (n + 1)).sum (λ (x : ℕ), f (⟨x, u⟩) * f (⟨n - x, v⟩))) 
  (hf : ∀ n m, f (n, m) ∈ 𝒜 n) : 
  galg_hom.is_homogeneous R (divided_power_algebra.grade R M) 𝒜 
    (lift_aux R M f hf_zero hf_smul hf_mul hf_add) := 
begin
  intros i a ha,
  dsimp [grade, quot_submodule] at ha,
  obtain ⟨p, hp, rfl⟩ := ha, 
  rw [← mkₐ_eq_mk R, lift_aux_eq, p.as_sum, eval₂_sum],
  apply _root_.sum_mem,
  intros c hc, 
  rw [eval₂_monomial, ← smul_eq_mul, algebra_map_smul A],
  apply submodule.smul_mem, 
  rw ← hp (mem_support_iff.mp hc), 
  exact finsupp.prod.mem_grade _ _ _ _ _ (λ ⟨n,m⟩ hnm, hf n m),
  { apply_instance }, 
end

variable {R}
  
lemma lift_is_homogeneous {A : Type*} [comm_ring A] [algebra R A] (𝒜 : ℕ → submodule R A) 
  [graded_algebra 𝒜] {I : ideal A} (hI : divided_powers I) (hI' : has_graded_dpow 𝒜 hI)
  (φ : M →ₗ[R] A) (hφ : ∀ m, φ m ∈ I) (hφ' : ∀ m, φ m ∈ 𝒜 1) : 
  galg_hom.is_homogeneous R (divided_power_algebra.grade R M) 𝒜 (lift R M hI φ hφ) := 
begin
  apply lift_aux_is_homogeneous,
  intros n m,
  simpa only [algebra.id.smul_eq_mul, mul_one] using hI' (φ m) (hφ m) 1 (hφ' m) n
end

lemma lift'_is_homogeneous {N : Type*} [decidable_eq N] [add_comm_group N] [module R N] 
  (f : M →ₗ[R] N) :
  galg_hom.is_homogeneous R (divided_power_algebra.grade R M) (divided_power_algebra.grade R N) 
    (lift' R R f) := 
begin
  apply lift_aux_is_homogeneous,
  { intro m, rw dp_zero},
  { intros n r m, rw [linear_map.map_smul, dp_smul] },
  { intros n p m, rw dp_mul }, 
  { intros n u v, rw [map_add, dp_add] },
  { intros n m, exact ⟨X(n, f m), ⟨is_weighted_homogeneous_X R _ _, rfl⟩⟩ },
end

/- We need the projections (divided_power_algebra R M) → grade R M n ,
more generally for graded algebras -/

variable (R)

def proj' (n : ℕ) : divided_power_algebra R M →ₗ[R] grade R M n := 
proj (grade R M) n

lemma proj'_zero_one : (proj' R M 0) 1 = 1 :=
by rw [proj', proj, linear_map.coe_mk, decompose_one]; refl

lemma proj'_zero_mul (x y : divided_power_algebra R M) : 
  (proj' R M 0) (x * y) = (proj' R M 0) x * (proj' R M 0) y :=
by simp only [proj', ← proj_zero_ring_hom'_apply, _root_.map_mul]

end decidable_eq 

instance : add_submonoid_class (submodule R (mv_polynomial (ℕ × M) R ⧸ relI R M))
  (divided_power_algebra R M) := 
submodule.add_submonoid_class

section grade_zero

variables (R)
def algebra_map_inv : divided_power_algebra R M →ₐ[R] R :=
lift R M (divided_powers.of_square_zero.divided_powers (by rw [zero_eq_bot, pow_two, bot_mul]))
  (0 : M →ₗ[R] R) (λ m, by simp only [linear_map.zero_apply, zero_eq_bot, mem_bot])

lemma algebra_map_inv_eq (f : mv_polynomial (ℕ × M) R) : 
  algebra_map_inv R M (mkₐ R (relI R M) f) = aeval (λ nm : ℕ × M, ite (0 < nm.1) (0 : R) 1) f :=
begin
  rw ← alg_hom.comp_apply, 
  apply alg_hom.congr_fun,
  ext ⟨n, m⟩,
  simp only [algebra_map_inv, alg_hom.comp_apply, lift_eqₐ, linear_map.zero_apply, aeval_X],
  by_cases hn : 0 < n,
  { rw [if_pos hn, eval₂_X, divided_powers.dpow_eval_zero _ (ne_of_gt hn)] },
  { rw if_neg hn,
    rw [not_lt, le_zero_iff] at hn,
    rw [hn, eval₂_X, divided_powers.dpow_zero _ (mem_bot.mpr rfl)] }
end

lemma proj'_zero_comp_algebra_map [decidable_eq R] [decidable_eq M] (x : R) :
  (((proj' R M 0) ∘ (algebra_map R (divided_power_algebra R M))) x).val =
  ((algebra_map R (divided_power_algebra R M))) x :=
begin
  rw [function.comp_app, subtype.val_eq_coe, proj', proj, linear_map.coe_mk,
    algebra.algebra_map_eq_smul_one, decompose_smul, decompose_one, dfinsupp.coe_smul,
    pi.smul_apply, submodule.coe_smul_of_tower],
  refl,
end

-- variables (M) 
lemma algebra_map_left_inverse :
  function.left_inverse (algebra_map_inv R M) (algebra_map R (divided_power_algebra R M)) := 
λ m, by simp only [alg_hom.commutes, algebra.id.map_eq_id, ring_hom.id_apply]

@[simp] lemma algebra_map_inj (x y : R) :
  algebra_map R (divided_power_algebra R M) x = algebra_map R (divided_power_algebra R M) y ↔ 
  x = y :=
(algebra_map_left_inverse R M).injective.eq_iff

@[simp] lemma algebra_map_eq_zero_iff (x : R) : 
  algebra_map R (divided_power_algebra R M) x = 0 ↔ x = 0 :=
map_eq_zero_iff (algebra_map _ _) (algebra_map_left_inverse _ _).injective

@[simp] lemma algebra_map_eq_one_iff (x : R) : 
  algebra_map R (divided_power_algebra R M) x = 1 ↔ x = 1 :=
map_eq_one_iff (algebra_map _ _) (algebra_map_left_inverse _ _).injective

lemma mkₐ_eq_aeval {C : Type*} [comm_ring C] {D : Type*} (I : ideal (mv_polynomial D C)) :
  (ideal.quotient.mkₐ C I) = aeval (λ (d : D), ideal.quotient.mk I (X d)) :=
by ext d; simp only [mkₐ_eq_mk, aeval_X]

lemma mk_eq_eval₂ {C : Type*} [comm_ring C] {D : Type*} (I : ideal (mv_polynomial D C)) :
  (ideal.quotient.mk I).to_fun  = eval₂ (algebra_map C ((mv_polynomial D C)⧸ I)) 
  (λ (d : D), ideal.quotient.mk I (X d)) :=
by ext d; simp_rw [ring_hom.to_fun_eq_coe, ← mkₐ_eq_mk C, mkₐ_eq_aeval, aeval_X]; refl

lemma algebra_map_right_inv_of_degree_zero [decidable_eq R] [decidable_eq M] (x : grade R M 0) :
  (algebra_map R (divided_power_algebra R M)) ((algebra_map_inv R M) x.1) = x.1 := 
begin
  have hx : x.val ∈ grade R M 0 := x.2,
  simp only [grade, quot_submodule, subtype.val_eq_coe, submodule.mem_map,
    mem_weighted_homogeneous_submodule, is_weighted_homogeneous] at hx,
  obtain ⟨p, hp0, hpx⟩ := hx,
  rw [subtype.val_eq_coe, ← hpx, algebra_map_inv_eq, mkₐ_eq_aeval, map_aeval, algebra.id.map_eq_id, 
    ring_hom_comp_triple.comp_eq, coe_eval₂_hom, aeval_def, p.as_sum, eval₂_sum, eval₂_sum, 
    finset.sum_congr rfl],
  intros exp hexp,
  have h : ∀ (nm : ℕ × M), nm ∈ exp.support → nm.fst = 0,
  { intros nm hnm, 
    specialize hp0 (mem_support_iff.mp hexp),
    rw [weighted_degree', finsupp.total_apply, finsupp.sum, finset.sum_eq_zero_iff] at hp0,
    specialize hp0 nm hnm,
    rw [algebra.id.smul_eq_mul, nat.mul_eq_zero] at hp0,
    exact or.resolve_left hp0 (finsupp.mem_support_iff.mp hnm), },
  rw [eval₂_monomial, eval₂_monomial],
  apply congr_arg,
  rw finsupp.prod_congr,
  intros nm hnm,
  rw [if_neg, ← @prod.mk.eta _ _ nm, ← dp_eq_mk, h nm hnm, dp_zero, map_one],
  { rw [h nm hnm], exact lt_irrefl 0, },
end

/-- An ideal J of a commutative ring A is an augmentation ideal
if ideal.quotient.mk J has a right inverse which is a ring_hom -/
def is_augmentation_ideal (A : Type*) [comm_ring A] (J : ideal A) : Prop :=
∃ g : A⧸J →+* A, (ideal.quotient.mk J) ∘ g = id

/-- The augmentation ideal in the divided_power_algebra -/
def aug_ideal : ideal (divided_power_algebra R M) := ring_hom.ker (algebra_map_inv R M)

lemma mem_aug_ideal_iff (f : divided_power_algebra R M) : 
  f ∈ aug_ideal R M ↔ algebra_map_inv R M f = 0 :=
by rw [aug_ideal, ring_hom.mem_ker]

/-- The image of ι is contained in the augmentation ideal -/
lemma ι_mem_aug_ideal (m : M) : (ι R) m ∈ aug_ideal R M :=
by simp only [mem_aug_ideal_iff, ι, dp, linear_map.coe_mk, algebra_map_inv_eq, aeval_X, 
  nat.lt_one_iff, eq_self_iff_true, if_true]

/- We prove that the augmentation is an augmentation ideal, namely there is a section -/
lemma aug_ideal_is_augmentation_ideal : 
  is_augmentation_ideal (divided_power_algebra R M) (aug_ideal R M) :=
begin
  let g := ker_lift_alg (algebra_map_inv R M),
  let g1 := algebra_map R (divided_power_algebra R M ⧸ aug_ideal R M),
  use (algebra_map R (divided_power_algebra R M)).comp g.to_ring_hom,
  ext x,
  rw [ker_lift_alg_to_ring_hom, ring_hom.coe_comp, function.comp_app, mk_algebra_map, id.def],
  suffices h_inv : function.right_inverse g g1, 
  { exact h_inv x },
  refine function.right_inverse_of_injective_of_left_inverse
   (ring_hom.ker_lift_injective _) _,
  intro r, 
  rw [alg_hom_class.commutes, algebra.id.map_eq_id, ring_hom.id_apply]
end 

-- Q : if algebra map has a section, is the kernel an augmentation ideal?

lemma coeff_zero_of_mem_aug_ideal {f : mv_polynomial (ℕ × M) R}
  (hf : f ∈ supported R {nm : ℕ × M | 0 < nm.fst}) (hf0 : (mk (relI R M)) f ∈ aug_ideal R M) : 
  coeff 0 f = 0 :=
begin
  rw [aug_ideal, ring_hom.mem_ker] at hf0,
  rw [← hf0, ← mkₐ_eq_mk R _, algebra_map_inv_eq R M, eq_comm],
  conv_lhs { rw [f.as_sum, map_sum] },
  convert @finset.sum_eq_single _ _ _ (f.support) _ 0 _ _,
  { rw [monomial_zero', aeval_C, algebra.id.map_eq_id, ring_hom.id_apply], },
  { intros b hb hb0,
    rw [aeval_monomial, algebra.id.map_eq_id, ring_hom.id_apply],
    convert mul_zero _,
    obtain ⟨i, hi⟩ := finsupp.support_nonempty_iff.mpr hb0,  
    rw finsupp.prod, 
    apply finset.prod_eq_zero hi,
    have hi' : 0 < i.fst,
    { apply mem_supported.mp hf,
      rw [finset.mem_coe, mem_vars],
      exact ⟨b, ⟨hb, hi⟩⟩ },
    rw if_pos hi',
    exact zero_pow (zero_lt_iff.mpr (finsupp.mem_support_iff.mp hi)) },
  { intro hf',
    rw [monomial_zero', aeval_C, algebra.id.map_eq_id, ring_hom.id_apply,
      ← not_mem_support_iff.mp hf'] }
end  

lemma aug_ideal_eq_span :
  span (set.image (λ nm, mk _ (X nm)) { nm : ℕ × M | 0 < nm.1 }) = aug_ideal R M := 
begin
  classical,
  apply le_antisymm,
  { rw span_le, 
    intros f,
    simp only [mk_eq_mk, set.mem_image, set.mem_set_of_eq, prod.exists, exists_and_distrib_left, 
      set_like.mem_coe, forall_exists_index, and_imp],
    intros n hn m hf, 
    rw [← hf, aug_ideal, ring_hom.mem_ker, algebra_map_inv, lift_eq],
    simp_rw linear_map.zero_apply,
    rw [eval₂_X, divided_powers.dpow_eval_zero _ (ne_of_gt hn)] },
  { intros f0 hf0,
    obtain ⟨⟨f, hf⟩, rfl⟩ := divided_power_algebra.surjective_of_supported R f0,
    have hf0' : coeff 0 f = 0 := coeff_zero_of_mem_aug_ideal R M hf hf0,
    simp only [alg_hom.coe_comp, mkₐ_eq_mk, subalgebra.coe_val, function.comp_app, 
      set_like.coe_mk] at hf0 ⊢,
    rw [set.image_comp, ← map_span (mk (relI R M)), f.as_sum],
    apply ideal.mem_map_of_mem _ (sum_mem _ _),
    intros c hc, 
    rw [monomial_eq, finsupp.prod],
    refine mul_mem_left _ _ _,
    suffices supp_ss : ↑(c.support) ⊆ {nm : ℕ × M | 0 < nm.fst},
    { by_cases hc0 : c.support.nonempty,
      { obtain ⟨nm, hnm⟩ := hc0,
        rw finset.prod_eq_mul_prod_diff_singleton hnm,
        apply mul_mem_right _ _ 
          (pow_mem_of_mem _ _ _ (nat.pos_of_ne_zero (finsupp.mem_support_iff.mp hnm))),
        exact subset_span ⟨nm, ⟨(supp_ss hnm), rfl⟩⟩ }, 
      { -- cas où c.support est vide : c = 0 ; contradiction
        rw [not_nonempty_iff_eq_empty, finsupp.support_eq_empty] at hc0,
        rw hc0 at hc, 
        exact absurd hf0' (mem_support_iff.mp hc) }},
    { -- supp_ss
      intros nm hnm, 
      apply mem_supported.mp hf, 
      simp only [mem_vars, mem_coe, mem_support_iff, ne.def, finsupp.mem_support_iff, exists_prop],
      rw [mem_coe, finsupp.mem_support_iff] at hnm,
      exact ⟨c,⟨mem_support_iff.mp hc, hnm⟩⟩ }}, 
end

lemma right_inv' [decidable_eq R] [decidable_eq M] (x : R) :
  (algebra_map_inv R M) (((proj' R M 0) ∘ (algebra_map R (divided_power_algebra R M))) x).val = x :=
by rw proj'_zero_comp_algebra_map; exact algebra_map_left_inverse R M x

lemma left_inv' [decidable_eq R] [decidable_eq M] (x : grade R M 0) :
  ((proj' R M 0) ∘ (algebra_map R (divided_power_algebra R M))) ((algebra_map_inv R M) x.val) = x :=
begin
  ext,
  simp only [proj', proj, linear_map.coe_mk, function.comp_app], 
  conv_rhs { rw [← subtype.val_eq_coe, ← direct_sum.decompose_of_mem_same _ x.2] },
  rw algebra_map_right_inv_of_degree_zero R M x,
end

/- grade R M 0 → R is isomorphism -/
noncomputable! def ring_equiv_degree_zero [decidable_eq R] [decidable_eq M] :
  ring_equiv (grade R M 0) R := 
{ to_fun    := λ x, algebra_map_inv R M x.1,
  inv_fun   := (proj' R M 0) ∘ (algebra_map R (divided_power_algebra R M)),
  left_inv  := left_inv' R M,
  right_inv := right_inv' R M,
  map_mul'  := λ x y, by rw ← _root_.map_mul; refl,
  map_add'  := λ x y, by rw ← _root_.map_add; refl, }

def proj_0_ring_hom [decidable_eq R] [decidable_eq M] : ring_hom (divided_power_algebra R M) R :=
{ to_fun    := (ring_equiv_degree_zero R M).to_fun ∘ (proj' R M 0),
  map_one'  := by rw [ring_equiv.to_fun_eq_coe, mul_equiv_class.map_eq_one_iff, proj'_zero_one],
  map_mul'  := λx y, by rw [ring_equiv.to_fun_eq_coe, function.comp_app, ← _root_.map_mul, 
    proj'_zero_mul],
  map_zero' := by simp only [ring_equiv.to_fun_eq_coe, function.comp_app, map_zero],
  map_add'  := by simp only [ring_equiv.to_fun_eq_coe, function.comp_app, map_add, 
    eq_self_iff_true, forall_const] }

end grade_zero

section grade_one

variable (R)
/-- The canonical map from `divided_power_algebra R M` into `triv_sq_zero_ext R M` that sends
`divided_power_algebra.ι` to `triv_sq_zero_ext.inr`. -/
def to_triv_sq_zero_ext [module Rᵐᵒᵖ M] [is_central_scalar R M] :
  divided_power_algebra R M →ₐ[R] triv_sq_zero_ext R M :=
lift R M (divided_powers.of_square_zero.divided_powers (triv_sq_zero_ext.square_zero R M) :
  divided_powers (ker_ideal R M)) (inr_hom R M) (λ m, (mem_ker_ideal_iff_exists R M _).mpr ⟨m, rfl⟩)

@[simp] lemma to_triv_sq_zero_ext_ι [module Rᵐᵒᵖ M] [is_central_scalar R M] (x : M) :
   to_triv_sq_zero_ext R M (ι R x) = inr x :=
lift_ι_apply R _ _ _ x

/- @[simp] lemma to_triv_sq_zero_ext_snd [module Rᵐᵒᵖ M] [is_central_scalar R M] (m : M) :
  (to_triv_sq_zero_ext R M) ((mkₐ R (relI R M)) p).snd = m :=
lift_ι_apply R _ _ _ x -/

.


lemma deg_one_left_inv [decidable_eq R] [decidable_eq M] [module Rᵐᵒᵖ M] [is_central_scalar R M] :
  function.left_inverse (λ (x : (grade R M 1)), (to_triv_sq_zero_ext R M x.1).snd) 
    ((proj' R M 1) ∘ (ι R)) :=
begin
  intros m,
  simp only [function.comp_app, subtype.val_eq_coe, ι, dp, proj', proj, linear_map.coe_mk],
  rw ← triv_sq_zero_ext.snd_inr R m, 
  apply congr_arg,
  rw [snd_inr, ← to_triv_sq_zero_ext_ι, ι, linear_map.coe_mk, dp, 
    decompose_of_mem_same _ (mkₐ_mem_grade R M 1 m)],
end

.

lemma deg_one_right_inv [decidable_eq R] [decidable_eq M] [module Rᵐᵒᵖ M] [is_central_scalar R M] :
  function.right_inverse (λ (x : (grade R M 1)), (to_triv_sq_zero_ext R M x.1).snd) 
    ((proj' R M 1) ∘ (ι R)) :=
begin
  intros x,
  /- have hx : x.val ∈ grade R M 1 := x.2,
  simp only [grade, quot_submodule, subtype.val_eq_coe, submodule.mem_map,
    mem_weighted_homogeneous_submodule, is_weighted_homogeneous] at hx,
  obtain ⟨p, hp1, hpx⟩ := hx, -/

  obtain ⟨q, hqx⟩ := surjective_of_supported R x.1,

  have hq1 : ∀ ⦃d : ℕ × M →₀ ℕ⦄, (weighted_degree' prod.fst) d ≠ 1 → coeff d q.1 = 0,
  { have hx := x.2,
    simp only [grade, quot_submodule, subtype.val_eq_coe, submodule.mem_map,
    mem_weighted_homogeneous_submodule, is_weighted_homogeneous] at hx,
    intros nm hnm,
    simp only [weighted_degree', ne.def] at hnm,
    sorry },

  have hq : rel R M (X (1, ((to_triv_sq_zero_ext R M)
          (((mkₐ R (relI R M)).comp (supported R {nm : ℕ × M | 0 < nm.fst}).val) q)).snd)) q.1,
  { rw alg_hom.comp_apply,
    simp only [subalgebra.coe_val, mkₐ_eq_mk, ← subtype.val_eq_coe],
    rw q.1.as_sum, rw map_sum, rw map_sum,
    by_cases h_supp : q.1.support = ∅,
    { rw h_supp, simp only [sum_empty, snd_zero],  rw ← zero_smul R (0 : M), 
      have hsmul : rel R M (X (1, (0 : R) • (0 : M))) _ := rel.smul,
      rw [pow_one, zero_smul R (X (1, (0 : M)))] at hsmul,
      exact hsmul,  },
    { rw [← ne.def, ← nonempty_iff_ne_empty] at h_supp,
      obtain ⟨a, ha⟩ := h_supp,
      have h_add : rel R M (X (1, ((q.1.support ∩ {a}).sum (λ (x : ℕ × M →₀ ℕ), 
        (to_triv_sq_zero_ext R M) ((mkₐ R (relI R M)) ((monomial x) (coeff x q.1)))) + 
        (q.1.support \ {a}).sum (λ (x : ℕ × M →₀ ℕ), (to_triv_sq_zero_ext R M) 
        ((mkₐ R (relI R M)) ((monomial x) (coeff x q.1))))).snd)) _ := rel.add,
      

      simp only [snd_add] at h_add,

      rw finset.range_add_one at h_add,
      rw finset.range_one at h_add,
      simp only [sum_insert, mem_singleton, nat.one_ne_zero, not_false_iff, 
        tsub_self, sum_singleton, tsub_zero] at h_add,
      
      rw ← finset.sum_inter_add_sum_diff q.1.support {a},

      


      /- convert h_add,

      simp only [subtype.val_eq_coe, support_sum_monomial_coeff, mkₐ_eq_mk],

      rw finset.range_add_one,
      rw finset.range_one,
      simp only [sum_insert, mem_singleton, nat.one_ne_zero, not_false_iff, 
        tsub_self, sum_singleton, tsub_zero], -/
    sorry
    }},

  ext,
  nth_rewrite 1 ← subtype.val_eq_coe,
  rw [← hqx],
  simp only [proj', proj, linear_map.coe_mk, function.comp_app, ι, dp],
  rw decompose_of_mem_same _ (mkₐ_mem_grade R M 1 _),
  conv_lhs {rw mkₐ_eq_aeval }, 
  rw aeval_def,
  simp only [eval₂_monomial, eval₂_X],
  rw alg_hom.comp_apply,
  rw mkₐ_eq_mk,

  rw ideal.quotient.eq,

  have h : X (1, ((to_triv_sq_zero_ext R M) x.val).snd) -
    (((supported R {nm : ℕ × M | 0 < nm.fst}).val) q) ∈ relI R M,
  { /- rw alg_hom.comp_apply,
    simp only [subalgebra.coe_val, mkₐ_eq_mk, ← subtype.val_eq_coe],
    rw q.1.as_sum, rw map_sum, rw map_sum, -/
    by_cases h_supp : q.1.support = ∅,
    { sorry/- rw h_supp, simp only [sum_empty, snd_zero],  rw ← zero_smul R (0 : M), 
      have hsmul : rel R M (X (1, (0 : R) • (0 : M))) _ := rel.smul,
      rw [pow_one, zero_smul R (X (1, (0 : M)))] at hsmul,
      exact hsmul,  -/ },
    { rw [← ne.def, ← nonempty_iff_ne_empty] at h_supp,
      obtain ⟨a, ha⟩ := h_supp,
      have h_add : rel R M (X (1, ((q.1.support ∩ {a}).sum (λ (x : ℕ × M →₀ ℕ), 
        (to_triv_sq_zero_ext R M) ((mkₐ R (relI R M)) ((monomial x) (coeff x q.1)))) + 
        (q.1.support \ {a}).sum (λ (x : ℕ × M →₀ ℕ), (to_triv_sq_zero_ext R M) 
        ((mkₐ R (relI R M)) ((monomial x) (coeff x q.1))))).snd)) _ := rel.add,

      -- simp only [snd_add] at h_add,
      rw finset.sum_inter_add_sum_diff q.1.support {a} at h_add,
      rw finset.range_add_one at h_add,
      rw finset.range_one at h_add,
      simp only [sum_insert, mem_singleton, nat.one_ne_zero, not_false_iff, 
        tsub_self, sum_singleton, tsub_zero] at h_add,
      rw ← map_sum at h_add, rw ← map_sum at h_add, 
      rw ← q.1.as_sum at h_add,
      --simp only [subtype.val_eq_coe, mkₐ_eq_mk] at h_add,
     -- simp_rw finset.sum_inter_add_sum_diff q.1.support {a} at h_add,
      have h_add' := sub_mem_rel_of_rel h_add,

      simp only [alg_hom.coe_comp, mkₐ_eq_mk, subalgebra.coe_val, 
        function.comp_app, subtype.val_eq_coe] at hqx,
      simp only [subtype.val_eq_coe]at h_add' ⊢,
      rw ← hqx,

     -- convert ideal.sub_mem _ h_add' _,
      sorry }},

  simp only [relI, of_rel, ← hqx],

  --simp only [subtype.val_eq_coe, ← hpx ],
  apply submodule.subset_span, 
  rw set.mem_set_of_eq,
  use (X (1, ((to_triv_sq_zero_ext R M)
    (((mkₐ R (relI R M)).comp (supported R {nm : ℕ × M | 0 < nm.fst}).val) q)).snd)),
  use ((supported R {nm : ℕ × M | 0 < nm.fst}).val) q,
  split,
  exact hq,
  rw sub_add_cancel,
 
end

#exit


lemma deg_one_right_inv' [decidable_eq R] [decidable_eq M] [module Rᵐᵒᵖ M] [is_central_scalar R M] :
  function.right_inverse (λ (x : (grade R M 1)), (to_triv_sq_zero_ext R M x.1).snd) 
    ((proj' R M 1) ∘ (ι R)) :=
begin
  intros x,
  have hx : x.val ∈ grade R M 1 := x.2,
  simp only [grade, quot_submodule, subtype.val_eq_coe, submodule.mem_map,
    mem_weighted_homogeneous_submodule, is_weighted_homogeneous] at hx,
  obtain ⟨p, hp1, hpx⟩ := hx,

  obtain ⟨q, hq⟩ := surjective_of_supported R x.1,

  have hp : rel R M (X (1, ((to_triv_sq_zero_ext R M) ((mkₐ R (relI R M)) p)).snd)) p,
  { rw p.as_sum, rw map_sum, rw map_sum,
    by_cases h_supp : p.support = ∅,
    { rw h_supp, simp only [sum_empty, snd_zero],  rw ← zero_smul R (0 : M), 
      have hsmul : rel R M (X (1, (0 : R) • (0 : M))) _ := rel.smul,
      rw [pow_one, zero_smul R (X (1, (0 : M)))] at hsmul,
      exact hsmul },
    { rw [← ne.def, ← nonempty_iff_ne_empty] at h_supp,
      obtain ⟨a, ha⟩ := h_supp,
      have h_add : rel R M (X (1, ((p.support ∩ {a}).sum (λ (x : ℕ × M →₀ ℕ), 
        (to_triv_sq_zero_ext R M) ((mkₐ R (relI R M)) ((monomial x) (coeff x p)))) + 
        (p.support \ {a}).sum (λ (x : ℕ × M →₀ ℕ), (to_triv_sq_zero_ext R M) 
        ((mkₐ R (relI R M)) ((monomial x) (coeff x p))))).snd)) _ := rel.add,
      

      simp only [snd_add] at h_add,
      
      rw ← finset.sum_inter_add_sum_diff p.support {a},
       
    sorry
    }},

  ext,
  rw [← hpx],
  simp only [proj', proj, linear_map.coe_mk, function.comp_app, ι, dp],
  rw decompose_of_mem_same _ (mkₐ_mem_grade R M 1 _),
  conv_lhs {rw mkₐ_eq_aeval }, 
  rw aeval_def,
  simp only [eval₂_monomial, eval₂_X],
  rw mkₐ_eq_mk,

  rw ideal.quotient.eq,

  simp only [relI, of_rel],
  simp only [subtype.val_eq_coe, ← hpx ],
  apply submodule.subset_span, 
  rw set.mem_set_of_eq,
  use X (1, ((to_triv_sq_zero_ext R M) ((mkₐ R (relI R M)) p)).snd),
  use p,
  split,
  exact hp,
  rw sub_add_cancel,
  --simp only [mkₐ_eq_mk, support_sum_monomial_coeff, exists_prop, set.mem_set_of_eq],

  --simp only [subtype.val_eq_coe, support_sum_monomial_coeff],


  --conv_rhs{ rw aeval_def, rw eval₂_sum,},

 -- simp only [eval₂_monomial, eval₂_X],
  --rw finset.sum_congr rfl,

  /- rw [subtype.val_eq_coe, ← hpx, algebra_map_inv_eq, mkₐ_eq_aeval, map_aeval, algebra.id.map_eq_id, 
    ring_hom_comp_triple.comp_eq, coe_eval₂_hom, aeval_def, p.as_sum, eval₂_sum, eval₂_sum, 
    finset.sum_congr rfl],-/
  /- simp_rw [subtype.val_eq_coe, ← hpx], 
  simp only [function.comp_app],
  simp only [proj', proj, linear_map.coe_mk, to_triv_sq_zero_ext, mkₐ_eq_aeval],
  rw [aeval_def, p.as_sum, eval₂_sum],
  simp only [eval₂_monomial],
  rw finset.sum_congr rfl, -/
  /-   rw [subtype.val_eq_coe, ← hpx, algebra_map_inv_eq, mkₐ_eq_aeval, map_aeval, algebra.id.map_eq_id, 
    ring_hom_comp_triple.comp_eq, coe_eval₂_hom, aeval_def, p.as_sum, eval₂_sum, eval₂_sum, 
    finset.sum_congr rfl],
  intros exp hexp,
  have h : ∀ (nm : ℕ × M), nm ∈ exp.support → nm.fst = 0,
  { intros nm hnm, 
    specialize hp0 (mem_support_iff.mp hexp),
    rw [weighted_degree', finsupp.total_apply, finsupp.sum, finset.sum_eq_zero_iff] at hp0,
    specialize hp0 nm hnm,
    rw [algebra.id.smul_eq_mul, nat.mul_eq_zero] at hp0,
    exact or.resolve_left hp0 (finsupp.mem_support_iff.mp hnm), },
  rw [eval₂_monomial, eval₂_monomial],
  apply congr_arg,
  rw finsupp.prod_congr,
  intros nm hnm,
  rw [if_neg, ← @prod.mk.eta _ _ nm, ← dp_eq_mk, h nm hnm, dp_zero, map_one],
  { rw [h nm hnm], exact lt_irrefl 0, }, -/
  /- simp only [to_triv_sq_zero_ext, function.comp_app, subtype.val_eq_coe,
    proj', proj, linear_map.coe_mk], -/
 --sorry, sorry
end

/- ι : M → grade R M 1 is isomorphism -/
def linear_equiv_degree_one [decidable_eq R] [decidable_eq M] [module Rᵐᵒᵖ M]
  [is_central_scalar R M] :  linear_equiv (ring_hom.id R) M (grade R M 1) :=
{ to_fun    := (proj' R M 1) ∘ ι R,
  inv_fun   := λ x, (to_triv_sq_zero_ext R M x.1).snd, 
  map_add'  := λ x y, by simp only [function.comp_app, map_add],
  map_smul' := λ r x, by simp only [function.comp_app, linear_map.map_smulₛₗ, ring_hom.id_apply],
  left_inv  := deg_one_left_inv R M,
  right_inv := deg_one_right_inv R M, }

/- 
-- Use description of augmentation ideal
/- ι : M → grade R M 1 is isomorphism -/
def linear_equiv_degree_one : linear_equiv (ring_hom.id R) M (grade R M 1) :=
{ to_fun    := (proj' R M 1) ∘ ι R,
  map_add'  := sorry,
  map_smul' := sorry,
  inv_fun   := sorry,
  left_inv  := sorry,
  right_inv := sorry } -/


end grade_one


variables (x : M) (n : ℕ)

/-- Lemma 2 of Roby 65. -/
lemma on_dp_algebra_unique (h h' : divided_powers (aug_ideal R M))
  (h1 : ∀ (x : M) (n : ℕ), h.dpow n (ι R x) = mk _ (X (n, x)))
  (h1' : ∀ (x : M) (n : ℕ), h'.dpow n (ι R x) = mk _ (X (n, x))) : h = h' := 
begin
  apply divided_powers.dp_uniqueness h h' (aug_ideal_eq_span R M),
  rintros n f ⟨⟨q, m⟩, hq, rfl⟩,
  simp only [set.mem_set_of_eq] at hq,
  simp only,
  nth_rewrite 0 [← h1 m q],
  rw [← h1' m q, h.dpow_comp n (ne_of_gt hq) (ι_mem_aug_ideal R M m), 
    h'.dpow_comp n (ne_of_gt hq) (ι_mem_aug_ideal R M m), h1 m,  h1' m],
end

def cond_D : Prop := ∃ (h : divided_powers (aug_ideal R M)), 
  ∀ (x : M) (n : ℕ), h.dpow n (ι R x) = submodule.quotient.mk (X (n, x))

end divided_power_algebra

end

section roby
/- Formalization of Roby 1965, section 8 -/

open_locale tensor_product

variables (A R S : Type*) [comm_ring A] [comm_ring R] [algebra A R] [comm_ring S] [algebra A S] 
  {I : ideal R} {J : ideal S} (hI : divided_powers I) (hJ : divided_powers J)

def i_1 : R →ₐ R ⊗[A] S := algebra.tensor_product.include_left

def i_2 : S →ₐ R ⊗[A] S := algebra.tensor_product.include_right

variables {R S} (I J)
def K : ideal (R ⊗[A] S) := (I.map (i_1 A R S)) ⊔ (J.map (i_2 A R S))

namespace divided_powers

variables {I J}
/- Lemma 1 : uniqueness of the dp structure on R ⊗ S for I + J -/
lemma on_tensor_product_unique (hK hK' : divided_powers (K A I J))
  (hIK : is_pd_morphism hI hK (i_1 A R S)) (hIK' : is_pd_morphism hI hK' (i_1 A R S))
  (hJK : is_pd_morphism hJ hK (i_2 A R S)) (hJK' : is_pd_morphism hJ hK' (i_2 A R S)) : 
  hK = hK' :=
begin
  apply eq_of_eq_on_ideal,
  intros n x hx,
  suffices : x ∈ sub_pd_ideal.pd_equalizer hK hK',  
  { exact ((sub_pd_ideal.mem_pd_equalizer_iff _ _).mp this).2 n,},
  suffices h_ss : K A I J ≤ sub_pd_ideal.pd_equalizer hK hK',
  { exact h_ss hx },
  dsimp only [K], 
  rw sup_le_iff,
  split,
  apply sub_pd_ideal.le_equalizer_of_pd_morphism hI (i_1 A R S).to_ring_hom
    le_sup_left hK hK' hIK hIK',
  apply sub_pd_ideal.le_equalizer_of_pd_morphism hJ (i_2 A R S).to_ring_hom
    le_sup_right hK hK' hJK hJK',
end

def cond_T : Prop :=
∃ hK : divided_powers (K A I J), 
  is_pd_morphism hI hK (i_1 A R S) ∧ is_pd_morphism hJ hK (i_2 A R S)

section free

-- hR_free, hS_free are not used for the def (they might be needed at lemmas about cond_T_free)
def cond_T_free /- [hR_free : module.free A R] [hS_free : module.free A S]  -/: Prop :=
∃ hK : divided_powers (K A I J), 
  is_pd_morphism hI hK (i_1 A R S) ∧ is_pd_morphism hJ hK (i_2 A R S)

def cond_Q (A R : Type*) [comm_ring A] [comm_ring R] /- [algebra A R] not used -/
  {I : ideal R} (hI : divided_powers I) : Prop := 
∃ (T : Type*) [comm_ring T], by exactI ∃ [algebra A T], by exactI ∃ [module.free A T]
  {J : ideal T} (hJ : divided_powers J) (f : pd_morphism hI hJ), 
  function.surjective f.to_ring_hom

end free

end divided_powers

end roby

section divided_power_algebra

/- 
and a divided power structure on that ideal such that
dpow R n (ι R x) = mk_alg_hom R (rel R M) (X (x, n)) 

(x,n) represents dpow n x
dpow m (x,n) should be dpow m (dpow n x) = (mchoose m n) dpow (m*n) x
An element x in divided_power_algebra R M takes the form
mk_alg_hom R (rel R M) (P)
where P is a sum of monomials  a * (m,n)   : m ∈ M, n ∈ ℕ
define
dpow k x = sum products a ^ kᵢ * dpow (mchoose kᵢ nᵢ (mᵢ,nᵢ * kᵢ)) 
where the sum is over functions → ℕ, with sum k
-/

/- Prove that it is unique… -/


/- Introduce notation ?
Here : x ^ [n] = mk_alg_hom R _ (X (x, n))
In general, x ^ [n]  for dpow n x ? 

-/

end divided_power_algebra